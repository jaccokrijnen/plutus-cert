Require Import PlutusCert.PlutusIR.
Require Import PlutusCert.Util.List.

Require Export PlutusCert.PlutusIR.Semantics.Static.Context.

(** Kinds of built-in types *)
Definition lookupBuiltinKind (u : DefaultUni) : kind :=
  match u with
  | DefaultUniInteger    => Kind_Base
  | DefaultUniByteString => Kind_Base
  | DefaultUniString     => Kind_Base
  | DefaultUniChar       => Kind_Base
  | DefaultUniUnit       => Kind_Base
  | DefaultUniBool       => Kind_Base
  end.

(** Kinding of types *)
Reserved Notation "Δ '|-*' T ':' K" (at level 40, T at level 0, K at level 0).
Inductive has_kind : list (string * kind) -> ty -> kind -> Prop :=
  | K_Var : forall Δ X K,
      lookup X Δ = Some K ->
      Δ |-* (Ty_Var X) : K
  | K_Fun : forall Δ T1 T2,
      Δ |-* T1 : Kind_Base ->
      Δ |-* T2 : Kind_Base ->
      Δ |-* (Ty_Fun T1 T2) : Kind_Base
  | K_IFix  : forall Δ F T K,
      Δ |-* T : K ->
      Δ |-* F : (Kind_Arrow (Kind_Arrow K Kind_Base) (Kind_Arrow K Kind_Base)) ->
      Δ |-* (Ty_IFix F T) : Kind_Base
  | K_Forall : forall Δ X K T,
      ((X, K) :: Δ) |-* T : Kind_Base ->
      Δ |-* (Ty_Forall X K T) : Kind_Base
  | K_Builtin : forall Δ u K,
      K = lookupBuiltinKind u ->
      Δ |-* (Ty_Builtin (Some' (TypeIn u))) : K
  | K_Lam : forall Δ X K1 T K2,
      ((X, K1) :: Δ) |-* T : K2 ->
      Δ |-* (Ty_Lam X K1 T) : (Kind_Arrow K1 K2)
  | K_App : forall Δ T1 T2 K1 K2,
      Δ |-* T1 : (Kind_Arrow K1 K2) ->
      Δ |-* T2 : K1 ->
      Δ |-* (Ty_App T1 T2) : K2
where "Δ '|-*' T ':' K" := (has_kind Δ T K).
